# Bazel sandbox image.

load("//src/main/docker:constants.bzl", "DEBIAN_JAVA_11")
load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_layer", "container_push")
load("@io_bazel_rules_docker//docker/util:run.bzl", "container_run_and_commit_layer")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")

JAVA = DEBIAN_JAVA_11

BASE_IMAGE = "@ubuntu_18_04//image"

BAZEL_IMAGE = "@bazel_image//image"

copy_file(
    name = "copy_golang_tar",
    src = "@golang//file",
    out = "golang.tar.gz",
    allow_symlink = True,
    tags = ["manual"],
)

container_image(
    name = "go_image",
    base = BASE_IMAGE,
    directory = "/usr/local",
    env = {"PATH": "$$PATH:/usr/local/go/bin"},
    tags = ["manual"],
    tars = [":golang.tar.gz"],
)

PACKAGES = [
    "git",
    "openjdk-11-jdk-headless",
    "python",
    "python3",
    "swig",
]

container_run_and_commit_layer(
    name = "install_packages",
    commands = [
        "apt-get update",
        "apt-get upgrade -y",
        "apt-get install -y --no-install-recommends " + " ".join(PACKAGES),
        "apt-get clean",

        # Install CUE.
        "go get -d -u cuelang.org/go/cmd/cue",
        "GOBIN=/usr/local/go/bin go install cuelang.org/go/cmd/cue",
    ],
    image = "go_image.tar",
    tags = [
        "manual",
        "no-remote-exec",
        "requires-network",
    ],
)

container_layer(
    name = "bazel_release_key",
    directory = "/etc/apt/trusted.gpg.d",
    files = ["bazel.gpg"],
    tags = ["manual"],
)

container_layer(
    name = "bazel_apt_list",
    directory = "/etc/apt/sources.list.d",
    files = ["bazel.list"],
    tags = ["manual"],
)

container_image(
    name = "bazel_apt_image",
    base = ":go_image",
    layers = [
        ":install_packages",
        ":bazel_release_key",
        ":bazel_apt_list",
    ],
    tags = ["manual"],
)

container_run_and_commit_layer(
    name = "install_bazel",
    commands = [
        "apt-get update",
        "apt-get install -y --no-install-recommends bazel",
        "apt-get clean",
    ],
    image = ":bazel_apt_image.tar",
    tags = [
        "manual",
        "no-remote-exec",
        "requires-network",
    ],
)

# Clean install based on BASE_IMAGE.
container_image(
    name = "bazel_clean_image",
    base = ":bazel_apt_image",
    env = JAVA.env,
    layers = [":install_bazel"],
    tags = ["manual"],
)

container_push(
    name = "push_bazel_clean_image",
    format = "Docker",
    image = ":bazel_clean_image",
    registry = "gcr.io",
    repository = "ads-open-measurement/bazel",
    tag = "clean",
    tags = [
        "manual",
        "requires-network",
    ],
)

container_run_and_commit_layer(
    name = "update_bazel",
    commands = [
        "apt-get update",
        "apt-get upgrade -y",
        "apt-get clean",
    ],
    image = BAZEL_IMAGE,
    tags = [
        "manual",
        "no-remote-exec",
        "requires-network",
    ],
)

# Updated image based on BAZEL_IMAGE.
container_image(
    name = "bazel_image",
    base = BAZEL_IMAGE,
    layers = [":update_bazel"],
    tags = ["manual"],
)

container_push(
    name = "push_bazel_image",
    format = "Docker",
    image = ":bazel_image",
    registry = "gcr.io",
    repository = "ads-open-measurement/bazel",
    tags = [
        "manual",
        "requires-network",
    ],
)
