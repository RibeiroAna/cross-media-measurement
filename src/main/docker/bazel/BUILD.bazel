# Bazel build environment image.

load("//src/main/docker:constants.bzl", "DEBIAN_JAVA_11")
load(
    "//src/main/docker:defs.bzl",
    "container_commit_add_apt_key",
    "container_commit_install_apt_packages",
)
load(
    "@io_bazel_rules_docker//container:container.bzl",
    "container_image",
    "container_layer",
    "container_push",
)

JAVA = DEBIAN_JAVA_11

BASE_IMAGE = "@ubuntu_18_04//image"

BAZEL_IMAGE = "@bazel_image//image"

# APT packages necessary for adding an APT key.
container_commit_install_apt_packages(
    name = "apt_key_packages",
    image = BASE_IMAGE,
    packages = [
        "apt-transport-https",
        "ca-certificates",
        "gnupg",
    ],
    tags = ["manual"],
)

container_layer(
    name = "bazel_key_file",
    directory = "/root",
    files = ["@bazel_apt_key//file"],
    mode = "0o644",
    tags = ["manual"],
)

container_image(
    name = "apt_key_install_image",
    base = BASE_IMAGE,
    layers = [
        ":apt_key_packages",
        ":bazel_key_file",
    ],
    tags = ["manual"],
)

container_commit_add_apt_key(
    name = "apt_keyring",
    image = ":apt_key_install_image.tar",
    key_path = "/root/bazel-release.pub.gpg",
    keyring_path = "/usr/share/keyrings/bazel.gpg",
    tags = ["manual"],
)

container_layer(
    name = "bazel_apt_list",
    directory = "/etc/apt/sources.list.d",
    files = ["bazel.list"],
    mode = "0o644",
    tags = ["manual"],
)

container_image(
    name = "package_install_image",
    base = BASE_IMAGE,
    layers = [
        ":apt_key_packages",
        ":apt_keyring",
        ":bazel_apt_list",
    ],
    tags = ["manual"],
)

container_commit_install_apt_packages(
    name = "apt_packages",
    image = ":package_install_image.tar",
    packages = [
        "bazel",
        "git",
        "openjdk-11-jdk-headless",
        "python",
        "python3",
        "swig",
        "tzdata",
    ],
    tags = ["manual"],
    upgrade = True,
)

container_image(
    name = "bazel_image",
    base = ":package_install_image",
    env = JAVA.env,
    layers = [
        ":apt_packages",
    ],
    stamp = True,
    tags = ["manual"],
    visibility = ["//visibility:public"],
)

container_push(
    name = "push_bazel_image",
    format = "Docker",
    image = ":bazel_image",
    registry = "gcr.io",
    repository = "ads-open-measurement/bazel",
    tags = [
        "manual",
        "requires-network",
    ],
)
